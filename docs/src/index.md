# DocumenterCitations.jl

```@eval
using Markdown
using Pkg

VERSION = Pkg.dependencies()[Base.UUID("daee34ce-89f3-4625-b898-19384cb65244")].version

github_badge = "[![Github](https://img.shields.io/badge/JuliaDocs-DocumenterCitations.jl-blue.svg?logo=github)](https://github.com/JuliaDocs/DocumenterCitations.jl)"

version_badge = "![v$VERSION](https://img.shields.io/badge/version-v$(replace("$VERSION", "-" => "--"))-green.svg)"

if get(ENV, "DOCUMENTER_BUILD_PDF", "") == ""
    Markdown.parse("$github_badge $version_badge")
else
    Markdown.parse("""
    -----

    On Github: [JuliaDocs/DocumenterCitations.jl](https://github.com/JuliaDocs/DocumenterCitations.jl)

    Version: $VERSION

    -----

    """)
end
```

[DocumenterCitations.jl](https://github.com/JuliaDocs/DocumenterCitations.jl#readme) uses [Bibliography.jl](https://github.com/Humans-of-Julia/Bibliography.jl) to add support for BibTeX citations in documentation pages generated by [Documenter.jl](https://github.com/JuliaDocs/Documenter.jl).

By default, [DocumenterCitations.jl](https://github.com/JuliaDocs/DocumenterCitations.jl#readme) uses a numeric citation style common in the natural sciences, see e.g. the [journals of the American Physical Society](https://journals.aps.org), and the [REVTeX author's guide](https://www.ctan.org/tex-archive/macros/latex/contrib/revtex/auguide). Citations are shown in-line, as a number enclosed in square brackets, e.g., "Optimal control is a cornerstone in the development of quantum technologies [BrifNJP2010](@cite)". [Alternative styles](@ref gallery) are supported, including an [author-year style](@ref author_year_style).

## Installation instructions

You can install the latest version of [DocumenterCitations.jl](https://github.com/JuliaDocs/DocumenterCitations.jl) using the [built-in package manager](https://docs.julialang.org/en/v1/stdlib/Pkg/)

```julia
pkg> add DocumenterCitations
```

In most cases, you will just want to have `DocumenterCitations` in the project that builds your documentation (e.g. [`test/Project.toml`](https://github.com/JuliaDocs/DocumenterCitations.jl/blob/master/test/Project.toml)). Thus, you can also simply add

```
DocumenterCitations = "daee34ce-89f3-4625-b898-19384cb65244"
```

to the `[deps]` section of the relevant `Project.toml` file.

## Telling Documenter.jl about your bibliography

First, place a BibTeX [`refs.bib`](./refs.bib) file in the `docs/src` folder of your project.  Then, in [`docs/make.jl`](https://github.com/JuliaDocs/DocumenterCitations.jl/blob/master/docs/make.jl), instantiate the [`CitationBibliography`](@ref) plugin with the path to the `.bib` file. Assuming `Documenter >= 1.0`, pass the plugin object to [`makedocs`](https://documenter.juliadocs.org/stable/lib/public/#Documenter.makedocs) as an element of the `plugins` keyword argument:

```julia
using DocumenterCitations

bib = CitationBibliography(
    joinpath(@__DIR__, "src", "refs.bib");
    style=:numeric
)
makedocs(; plugins=[bib], ...)
```

In older versions of [Documenter.jl](https://github.com/JuliaDocs/Documenter.jl), `bib` had to be passed as a positional argument to `makedocs`.

To use the [author-year style](@ref author_year_style) that was the default prior to `DocumenterCitations` version 1.0, replace `style=:numeric` with `style=:authoryear`.

Somewhere in your documentation, like a [References](@ref) page, include a markdown block

~~~markdown
# References

```@bibliography
```
~~~

to insert the bibliography for all cited references in the project. See [Syntax for the Bibliography Block](@ref) for more options.

You will also want to add custom [CSS Styling](@ref) to your documentation to improve the rendering of your bibliography.

Thus, a [full `docs/make.jl` file](https://github.com/JuliaDocs/DocumenterCitations.jl/blob/master/docs/make.jl) might look something like this:

```julia
using DocumenterCitations
using Documenter
using Pkg

PROJECT_TOML = Pkg.TOML.parsefile(joinpath(@__DIR__, "..", "Project.toml"))
VERSION = PROJECT_TOML["version"]
NAME = PROJECT_TOML["name"]
AUTHORS = join(PROJECT_TOML["authors"], ", ") * " and contributors"
GITHUB = "https://github.com/JuliaDocs/DocumenterCitations.jl"

bib = CitationBibliography(
    joinpath(@__DIR__, "src", "refs.bib"),
    style=:numeric  # default
)

makedocs(;
    authors=AUTHORS,
    sitename="DocumenterCitations.jl",
    format=Documenter.HTML(
        prettyurls=true,
        canonical="https://juliaquantumcontrol.github.io/DocumenterCitations.jl",
        assets=String["assets/citations.css"],
        footer="[$NAME.jl]($GITHUB) v$VERSION docs powered by [Documenter.jl](https://github.com/JuliaDocs/Documenter.jl)."
    ),
    pages=[
        "Home"                   => "index.md",
        "Syntax"                 => "syntax.md",
        "Citation Style Gallery" => "gallery.md",
        "CSS Styling"            => "styling.md",
        "Internals"              => "internals.md",
        "References"             => "references.md",
    ],
    plugins=[bib],
)
deploydocs(; repo="github.com/JuliaDocs/DocumenterCitations.jl.git")
```

Bibliographies are also supported in [PDFs generated via LaTeX](https://documenter.juliadocs.org/stable/man/other-formats/#pdf-output). All that is required is to replace `format=Documenter.HTML(…)` in the above code with `format=Documenter.LaTeX()`.  See [`docs/makepdf.jl`](https://github.com/JuliaDocs/DocumenterCitations.jl/blob/master/docs/makepdf.jl) for an example. The resulting PDF files for the `DocumenterCitations` package are available as attachments to the [Releases](https://github.com/JuliaDocs/DocumenterCitations.jl/releases).

The rendering of the documentation may be fine-tuned using the [`DocumenterCitations.set_latex_options`](@ref) function. Note that the bibliography in LaTeX is directly rendered for the [different styles](@ref gallery) from the same internal representation as the HTML version. In particular, `bibtex`/`biblatex` is not involved in producing the PDF.


## How to cite references in your documentation

You can put citations anywhere in your docs, both in the markdown pages and in the docstrings of any functions that are shown as part of the API documentation: The basic syntax is, e.g., `[GoerzQ2022](@cite)`, for a BibTeX key [GoerzQ2022](@cite GoerzQ2022) in [`refs.bib`](./refs.bib),  which will be rendered in the default numeric style as "[GoerzQ2022](@cite)".  See [Syntax for Citations](@ref) for more details.

Clicking on the citations takes you to the bibliography ("[References](@ref)").

## Examples

The following is a list of some projects that use `DocumenterCitations`:

* [Oceananigans](https://clima.github.io/OceananigansDocumentation/stable/)
* [OSCAR](https://docs.oscar-system.org/stable/)
* [QuantumPropagators](https://juliaquantumcontrol.github.io/QuantumPropagators.jl/)
* [QuantumControl](https://juliaquantumcontrol.github.io/QuantumControl.jl/)
* [TwoQubitWeylChamber](https://juliaquantumcontrol.github.io/TwoQubitWeylChamber.jl/)
* [QuantumClifford](https://quantumsavory.github.io/QuantumClifford.jl/stable/references/) and the parent [QuantumSavory organization](https://github.com/QuantumSavory)

## Home References

This page cites the following references:

```@bibliography
Pages = ["index.md"]
Canonical = false
```

Also see the [full bibliography](@ref References) for further references cited throughout this documentation.
